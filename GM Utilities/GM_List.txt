prontera,134,172,5	script	GM List	4_F_KAFRA9,{
	function fun_GMTitle;
	function func_GMState;
	
	.@size = query_sql("SELECT `char`.`account_id`,`char`.`char_id`,`char`.`name`,`login`.`lastlogin`,TIMEDIFF(NOW(),`login`.`lastlogin`),`login`.`group_id` FROM `char` INNER JOIN `login` ON `char`.`account_id` = `login`.`account_id` WHERE `group_id` > "+.min_group_id+" ORDER BY `char`.`online` DESC, `login`.`group_id` DESC, `char`.`name` DESC LIMIT "+.max_limit,.@aid,.@cid,.@name$,.@lastlogin$,.@lastlogin_duration$,.@group_id);

	cutin "kafra_09",2;
	mes "There are total ^FF0000"+.@size+"^000000 GM(s)";
	while (.@i < .@size) {
		mes fun_GMTitle(.@name$[.@i],.@group_id[.@i])+" - "+func_GMState(.@aid[.@i],.@cid[.@i],.@name$[.@i]);
		//mes "Last Seen: ^777777"+.@lastlogin_duration$[.@i]+" hours ago^000000";
		//mes "Last Login: ^777777"+.@lastlogin$[.@i]+"^000000";
		//mes " ";
		.@i++;
	}
	close2;
	cutin "",255;
	end;
	
	OnInit:
		.max_limit = 10;
		.min_group_id = 1;
		.afk_second = 300;
		end;

	function	fun_GMTitle	{
		switch (getarg(1,0)) {
			default: .@title$ = ""; break;
			case 1: .@title$ = "(Super Player)"; break;
			case 2: .@title$ = "(Support)"; break;
			case 3: .@title$ = "(Script Manager)"; break;
			case 4: .@title$ = "(Event Manager)"; break;
			case 10: .@title$ = "(^FF0000Law Enforcement^000000)"; break;
			case 99: .@title$ = "(^fcba03Admin^000000)"; break;
		}
		
		return .@title$ + " " +  getarg(0,"NULL");
	}
	
	function	func_GMState	{
		.@aid = getarg(0,0);
		.@cid = getarg(1,0);
		.@name$ = getarg(2,"NULL");
		
		if (isloggedin(.@aid,.@cid)) {
		
			.@idle = checkidle(.@name$);
			
			if (.@idle >= .afk_second && .afk_second) {
				.@state$ = "AFK/Away";
			}
			else {
				.@state$ = "Online";
			}
		}
		else {
			.@state$ = "^FF0000Offline^000000";
		}
		
		return .@state$;
	}

}